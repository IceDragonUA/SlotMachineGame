apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion androidCompileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId androidApplicationId
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
    }

    flavorDimensions "version"

    productFlavors {
        development {
            buildConfigField "String", "FLAVOR", "\"$devFlavor\""
        }
        production {
            buildConfigField "String", "FLAVOR", "\"$prodFlavor\""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == "release" && variant.productFlavors[0].name == "development") {
                variant.outputs.each { output ->
                    output.outputFileName = output.outputFile.name.replace(".apk", "-${versionCode}-" + devFlavor.charAt(0) + ".apk")
                }
            } else if(variant.buildType.name == "release" && variant.productFlavors[0].name == "production") {
                variant.outputs.each { output ->
                    output.outputFileName = output.outputFile.name.replace(".apk", "-${versionCode}-" + prodFlavor.charAt(0) + ".apk")
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation appDependencies.kotlinStd
    implementation appDependencies.appCompat
    implementation appDependencies.coreKtx
    implementation appDependencies.multidex
    implementation appDependencies.koin
    implementation appDependencies.anko
    implementation appDependencies.material
    implementation appDependencies.lifecycleExtentions
    kapt appDependencies.lifecycleCompiler
    implementation appDependencies.navigationUi
    implementation appDependencies.navigationFragment
    implementation appDependencies.navigationFeature
    implementation appDependencies.glide
    kapt appDependencies.glideCompiler
    implementation appDependencies.constraint
    implementation appDependencies.timber
    implementation appDependencies.rxJava
    implementation appDependencies.rxAndroid
}
